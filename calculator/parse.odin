#+feature dynamic-literals
package calculator

import "core:io"
import "core:strconv"
import "core:fmt"
import "core:strings"

Version :: enum {
	std140,
	std430,
}

selected_version: Version

parse_and_calculate :: proc(input: string, out: io.Writer, err: io.Writer, version: Version) -> bool {

	selected_version = version

	lines := strings.split(input, "\n", context.temp_allocator)
	blocks: [dynamic]^Block

	// initial block
	current_block: ^Block = new(Block)
	current_block.title = ""
	append(&blocks, current_block)

	// read lines
	has_error := false
	line_loop: for &line, i in lines {

		// check if we start a new block
		if strings.index(line, "#") != -1 {

			// start new block
			if !(len(current_block.elements) == 0 && len(blocks) == 1) {
				current_block = new(Block)
				append(&blocks, current_block)
			}

			// get title
			space_idx := strings.index(line, " ")
			if space_idx == -1 {
				fmt.wprintfln(err, "Bad new block directive at line: %v", i)
				has_error = true
				continue line_loop
			}
			current_block.title = line[space_idx + 1:]

			// get type
			if strings.index(line, "#struct") != -1 {
				current_block.type = .Structure
				type_map[current_block.title] = current_block
			} else if strings.index(line, "#uniform") != -1 {
				current_block.type = .Uniform
			} else {
				fmt.wprintfln(err, "Bad new block directive at line: %v", i)
				has_error = true
				continue line_loop
			}

			continue line_loop
		}

		// discard after semicolon
		semicolon_idx := strings.index(line, ";")
		if semicolon_idx != -1 do line = line[:semicolon_idx]

		// get fields
		fields := strings.fields(line, context.temp_allocator)
		if len(fields) == 0 do continue line_loop
		else if len(fields) != 2 {
			fmt.wprintfln(err, "Wrong number of fields on line: %v", i)
			has_error = true
			continue line_loop
		}

		// parse first half
		type, ok := type_map[fields[0]]
		if !ok {
			fmt.wprintfln(err, "Unknown type '%v' on line: %v", fields[0], i)
			has_error = true
			continue line_loop
		}

		// parse second half
		second_half := fields[1]

		// check if there is an array in the name
		array_start := strings.index(second_half, "[")
		array_end := strings.index(second_half, "]")
		if array_start != -1 && array_end != -1 {
			if array_start + 1 >= array_end {
				fmt.wprintfln(err, "Messed up array on line: %v", i)
				has_error = true
				continue line_loop
			}
			
			// parse array size
			array_size, ok := strconv.parse_int(second_half[array_start + 1 : array_end])
			if !ok {
				fmt.wprintfln(err, "Invalid array size on line: %v", i)
				has_error = true
				continue line_loop
			}

			// make array based on type
			switch t in type {
			case Scalar:
				type = Array { type = t, size = array_size }
			case Vector:
				type = Array { type = t, size = array_size }
			case Matrix:
				type = Array { type = t, size = array_size }
			case Structure:
				type = Array { type = t, size = array_size }
			case Array:
				fmt.wprintfln(err, "Cannot have an array of arrays on line: %v", i)
				has_error = true
				continue line_loop
			}
		} else if array_start != -1 || array_end != -1 {
			fmt.wprintfln(err, "Unbalanced array on line: %v", i)
			has_error = true
			continue line_loop
		}

		append(&current_block.elements, Block_Element{ name = line, type = type })
	}

	if has_error do return false

	// Send output
	for block in blocks {

		calc := calculate_block(block)

		target_size: int
		switch block.type {
		case .Uniform:
			target_size = calc.total_size
			if block.title != "" do fmt.wprintfln(out, "Uniform %v: (size %v)", block.title, target_size)
			
		case .Structure:
			target_size = get_size(block)  // block is a valid Structure
			fmt.wprintfln(out, "Struct %v: (size %v)", block.title, target_size)
		}

		prev_end := 0
		for e, i in block.elements {
			if calc.offsets[i] > prev_end {
				fmt.wprintfln(out, "%v-%v: IMPLICIT PADDING", prev_end, calc.offsets[i] - 1)
			}
			prev_end = calc.offsets[i] + calc.sizes[i]
			fmt.wprintfln(out, "%v-%v: %v", calc.offsets[i], calc.offsets[i] + calc.sizes[i] - 1, e.name)
		}
		if block.type == .Structure && target_size > prev_end {
			fmt.wprintfln(out, "%v-%v: STRUCT PADDING", prev_end, target_size - 1)
		}

		fmt.wprintln(out, "")
	}

	return true
}

@(private = "package")
type_map := map[string]Type{
	"bool"    = .single,
	"float"   = .single,
	"int"     = .single,
	"uint"    = .single,
	"double"  = .double,

	"vec2"    = Vector{ type = .single, count = .two },
	"vec3"    = Vector{ type = .single, count = .three },
	"vec4"    = Vector{ type = .single, count = .four },

	"bvec2"   = Vector{ type = .single, count = .two },
	"bvec3"   = Vector{ type = .single, count = .three },
	"bvec4"   = Vector{ type = .single, count = .four },

	"ivec2"   = Vector{ type = .single, count = .two },
	"ivec3"   = Vector{ type = .single, count = .three },
	"ivec4"   = Vector{ type = .single, count = .four },

	"uvec2"   = Vector{ type = .single, count = .two },
	"uvec3"   = Vector{ type = .single, count = .three },
	"uvec4"   = Vector{ type = .single, count = .four },

	"dvec2"   = Vector{ type = .double, count = .two },
	"dvec3"   = Vector{ type = .double, count = .three },
	"dvec4"   = Vector{ type = .double, count = .four },

	"mat2"    = Matrix{ type = .single, major = .two,   minor = .two },
	"mat3"    = Matrix{ type = .single, major = .three, minor = .three },
	"mat4"    = Matrix{ type = .single, major = .four,  minor = .four },

	"mat2x2"  = Matrix{ type = .single, major = .two,   minor = .two },
	"mat2x3"  = Matrix{ type = .single, major = .two,   minor = .three },
	"mat2x4"  = Matrix{ type = .single, major = .two,   minor = .four },

	"mat3x2"  = Matrix{ type = .single, major = .three, minor = .two },
	"mat3x3"  = Matrix{ type = .single, major = .three, minor = .three },
	"mat3x4"  = Matrix{ type = .single, major = .three, minor = .four },

	"mat4x2"  = Matrix{ type = .single, major = .four,  minor = .two },
	"mat4x3"  = Matrix{ type = .single, major = .four,  minor = .three },
	"mat4x4"  = Matrix{ type = .single, major = .four,  minor = .four },

	"dmat2"   = Matrix{ type = .double, major = .two,   minor = .two },
	"dmat3"   = Matrix{ type = .double, major = .three, minor = .three },
	"dmat4"   = Matrix{ type = .double, major = .four,  minor = .four },

	"dmat2x2" = Matrix{ type = .double, major = .two,   minor = .two },
	"dmat2x3" = Matrix{ type = .double, major = .two,   minor = .three },
	"dmat2x4" = Matrix{ type = .double, major = .two,   minor = .four },

	"dmat3x2" = Matrix{ type = .double, major = .three, minor = .two },
	"dmat3x3" = Matrix{ type = .double, major = .three, minor = .three },
	"dmat3x4" = Matrix{ type = .double, major = .three, minor = .four },

	"dmat4x2" = Matrix{ type = .double, major = .four,  minor = .two },
	"dmat4x3" = Matrix{ type = .double, major = .four,  minor = .three },
	"dmat4x4" = Matrix{ type = .double, major = .four,  minor = .four },
}
